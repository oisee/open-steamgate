CLASS zcl_stg_model_info DEFINITION PUBLIC CREATE PUBLIC.
* Runs the registered MPC once per service and keeps what the dispatcher and
* the serializer need: entity sets, their types, properties, keys.
  PUBLIC SECTION.
    TYPES: BEGIN OF ty_property,
             name      TYPE string,
             fieldname TYPE string,
             edm_type  TYPE string,
             is_key    TYPE abap_bool,
             nullable  TYPE abap_bool,
           END OF ty_property.
    TYPES ty_properties TYPE STANDARD TABLE OF ty_property WITH DEFAULT KEY.

    TYPES: BEGIN OF ty_nav,
             name        TYPE string,
             association TYPE string,
             target_type TYPE string,
             target_set  TYPE string,
             to_many     TYPE abap_bool,
           END OF ty_nav.
    TYPES ty_navs TYPE STANDARD TABLE OF ty_nav WITH DEFAULT KEY.

    TYPES: BEGIN OF ty_entity_set,
             name        TYPE string,
             entity_type TYPE string,
             properties  TYPE ty_properties,
             navs        TYPE ty_navs,
           END OF ty_entity_set.
    TYPES ty_entity_sets TYPE STANDARD TABLE OF ty_entity_set WITH DEFAULT KEY.

    TYPES: BEGIN OF ty_association,
             name              TYPE string,
             left_type         TYPE string,
             right_type        TYPE string,
             left_card         TYPE string,
             right_card        TYPE string,
             left_set          TYPE string,
             right_set         TYPE string,
             principal_is_left TYPE abap_bool,
             pairs             TYPE zcl_oao_ref_constraint=>ty_pairs,
           END OF ty_association.
    TYPES ty_associations TYPE STANDARD TABLE OF ty_association WITH DEFAULT KEY.

    TYPES: BEGIN OF ty_service,
             name         TYPE string,
             namespace    TYPE string,
             entity_sets  TYPE ty_entity_sets,
             associations TYPE ty_associations,
           END OF ty_service.

    CLASS-METHODS get
      IMPORTING
        iv_service        TYPE string
      RETURNING
        VALUE(rs_service) TYPE ty_service
      RAISING
        zcx_stg_error.

    CLASS-METHODS find_set
      IMPORTING
        is_service    TYPE ty_service
        iv_entity_set TYPE string
      RETURNING
        VALUE(rs_set) TYPE ty_entity_set
      RAISING
        zcx_stg_error.

    CLASS-METHODS key_names
      IMPORTING
        is_set          TYPE ty_entity_set
      RETURNING
        VALUE(rt_names) TYPE string_table.

    CLASS-METHODS find_property
      IMPORTING
        is_set             TYPE ty_entity_set
        iv_name            TYPE string
      RETURNING
        VALUE(rs_property) TYPE ty_property
      RAISING
        zcx_stg_error.

    CLASS-METHODS find_nav
      IMPORTING
        is_set        TYPE ty_entity_set
        iv_name       TYPE string
      RETURNING
        VALUE(rs_nav) TYPE ty_nav
      RAISING
        zcx_stg_error.

    CLASS-METHODS clear.
  PRIVATE SECTION.
    CLASS-DATA gt_services TYPE HASHED TABLE OF ty_service WITH UNIQUE KEY name.

    CLASS-METHODS build
      IMPORTING
        iv_service        TYPE string
      RETURNING
        VALUE(rs_service) TYPE ty_service
      RAISING
        zcx_stg_error.
ENDCLASS.

CLASS zcl_stg_model_info IMPLEMENTATION.

  METHOD get.
    DATA lv_service TYPE string.

    lv_service = to_upper( iv_service ).
    READ TABLE gt_services INTO rs_service WITH TABLE KEY name = lv_service.
    IF sy-subrc <> 0.
      rs_service = build( lv_service ).
      INSERT rs_service INTO TABLE gt_services.
    ENDIF.
  ENDMETHOD.

  METHOD clear.
    CLEAR gt_services.
  ENDMETHOD.

  METHOD build.
    DATA lo_mpc         TYPE REF TO /iwbep/cl_mgw_push_abs_model.
    DATA lo_model       TYPE REF TO zcl_oao_model.
    DATA lt_type_names  TYPE zcl_oao_model=>ty_entity_names.
    DATA lv_type_name   LIKE LINE OF lt_type_names.
    DATA lo_entity      TYPE REF TO zcl_oao_entity_typ.
    DATA lt_properties  TYPE /iwbep/if_mgw_med_odata_types=>ty_t_mgw_odata_properties.
    DATA ls_property    LIKE LINE OF lt_properties.
    DATA lo_property    TYPE REF TO zcl_oao_property.
    DATA ls_info        TYPE ty_property.
    DATA lt_info        TYPE ty_properties.
    DATA lt_entity_sets TYPE zcl_oao_entity_typ=>ty_entity_sets.
    DATA ls_entity_set  LIKE LINE OF lt_entity_sets.
    DATA ls_set         TYPE ty_entity_set.
    DATA lx_gateway     TYPE REF TO /iwbep/cx_mgw_base_exception.
    DATA lt_assocs      TYPE zcl_oao_model=>ty_associations.
    DATA lo_assoc       TYPE REF TO zcl_oao_association.
    DATA lt_assoc_sets  TYPE zcl_oao_model=>ty_assoc_sets.
    DATA lo_assoc_set   TYPE REF TO zcl_oao_assoc_set.
    DATA ls_association TYPE ty_association.
    DATA lt_navs        TYPE zcl_oao_entity_typ=>ty_nav_props.
    DATA lo_nav         TYPE REF TO zcl_oao_nav_prop.
    DATA ls_nav         TYPE ty_nav.
    DATA ls_other       TYPE ty_entity_set.
    FIELD-SYMBOLS <ls_set> TYPE ty_entity_set.

    rs_service-name = iv_service.
    TRY.
        lo_mpc = zcl_oao_registry=>create_mpc( iv_service ).
        lo_mpc->define( ).
      CATCH /iwbep/cx_mgw_base_exception INTO lx_gateway.
        RAISE EXCEPTION TYPE zcx_stg_error
          EXPORTING
            status   = 404
            code     = 'STG/SERVICE_NOT_FOUND'
            message  = |Service { iv_service } is not registered|
            previous = lx_gateway.
    ENDTRY.

    lo_model ?= lo_mpc->model.
    lo_model->/iwbep/if_mgw_odata_model~get_schema_namespace( IMPORTING ev_namespace = rs_service-namespace ).
    lt_type_names = lo_model->get_entity_type_names( ).

    LOOP AT lt_type_names INTO lv_type_name.
      TRY.
          lo_entity ?= lo_model->/iwbep/if_mgw_odata_model~get_entity_type( lv_type_name ).
        CATCH /iwbep/cx_mgw_med_exception INTO lx_gateway.
          RAISE EXCEPTION TYPE zcx_stg_error
            EXPORTING
              status   = 500
              code     = 'STG/MODEL'
              message  = |Entity type { lv_type_name } vanished from the model|
              previous = lx_gateway.
      ENDTRY.

      CLEAR lt_info.
      lt_properties = lo_entity->/iwbep/if_mgw_odata_entity_typ~get_properties( ).
      LOOP AT lt_properties INTO ls_property.
        lo_property ?= ls_property-property.
        CLEAR ls_info.
        ls_info-name      = ls_property-name.
        ls_info-fieldname = to_upper( lo_property->mv_abap_fieldname ).
        IF ls_info-fieldname IS INITIAL.
          ls_info-fieldname = to_upper( ls_property-name ).
        ENDIF.
        ls_info-edm_type = lo_property->mv_edm_type.
        IF ls_info-edm_type IS INITIAL.
          ls_info-edm_type = /iwbep/if_mgw_med_odata_types=>gcs_edm_data_types-string.
        ENDIF.
        ls_info-is_key   = lo_property->mv_is_key.
        ls_info-nullable = lo_property->mv_nullable.
        APPEND ls_info TO lt_info.
      ENDLOOP.

      lt_entity_sets = lo_entity->get_entity_sets( ).
      LOOP AT lt_entity_sets INTO ls_entity_set.
        CLEAR ls_set.
        ls_set-name        = ls_entity_set-name.
        ls_set-entity_type = lv_type_name.
        ls_set-properties  = lt_info.
        APPEND ls_set TO rs_service-entity_sets.
      ENDLOOP.
    ENDLOOP.

* associations and their sets
    lt_assocs     = lo_model->get_associations( ).
    lt_assoc_sets = lo_model->get_association_sets( ).
    LOOP AT lt_assocs INTO lo_assoc.
      CLEAR ls_association.
      ls_association-name       = lo_assoc->mv_name.
      ls_association-left_type  = lo_assoc->mv_left_type.
      ls_association-right_type = lo_assoc->mv_right_type.
      ls_association-left_card  = lo_assoc->mv_left_card.
      ls_association-right_card = lo_assoc->mv_right_card.
      IF lo_assoc->mo_ref_constraint IS BOUND.
        ls_association-principal_is_left = lo_assoc->mo_ref_constraint->mv_principal_is_left.
        ls_association-pairs             = lo_assoc->mo_ref_constraint->mt_pairs.
      ENDIF.
      LOOP AT lt_assoc_sets INTO lo_assoc_set.
        IF lo_assoc_set->mv_association = lo_assoc->mv_name.
          ls_association-left_set  = lo_assoc_set->mv_left_set.
          ls_association-right_set = lo_assoc_set->mv_right_set.
        ENDIF.
      ENDLOOP.
      APPEND ls_association TO rs_service-associations.
    ENDLOOP.

* navigation properties, resolved to a target set per entity set
    LOOP AT rs_service-entity_sets ASSIGNING <ls_set>.
      lv_type_name = <ls_set>-entity_type.
      TRY.
          lo_entity ?= lo_model->/iwbep/if_mgw_odata_model~get_entity_type( lv_type_name ).
        CATCH /iwbep/cx_mgw_med_exception.
          CONTINUE.
      ENDTRY.
      lt_navs = lo_entity->get_navigation_properties( ).
      LOOP AT lt_navs INTO lo_nav.
        CLEAR ls_nav.
        ls_nav-name        = lo_nav->mv_name.
        ls_nav-association = lo_nav->mv_association.
        READ TABLE rs_service-associations INTO ls_association WITH KEY name = lo_nav->mv_association.
        IF sy-subrc <> 0.
          CONTINUE.
        ENDIF.
        IF ls_association-left_type = <ls_set>-entity_type.
          ls_nav-target_type = ls_association-right_type.
          ls_nav-target_set  = ls_association-right_set.
          IF ls_association-right_card <> '1' AND ls_association-right_card <> '0'.
            ls_nav-to_many = abap_true.
          ENDIF.
        ELSE.
          ls_nav-target_type = ls_association-left_type.
          ls_nav-target_set  = ls_association-left_set.
          IF ls_association-left_card <> '1' AND ls_association-left_card <> '0'.
            ls_nav-to_many = abap_true.
          ENDIF.
        ENDIF.
        IF ls_nav-target_set IS INITIAL.
          LOOP AT rs_service-entity_sets INTO ls_other WHERE entity_type = ls_nav-target_type.
            ls_nav-target_set = ls_other-name.
            EXIT.
          ENDLOOP.
        ENDIF.
        APPEND ls_nav TO <ls_set>-navs.
      ENDLOOP.
    ENDLOOP.
  ENDMETHOD.

  METHOD find_nav.
    READ TABLE is_set-navs INTO rs_nav WITH KEY name = iv_name.
    IF sy-subrc <> 0.
      RAISE EXCEPTION TYPE zcx_stg_error
        EXPORTING
          status  = 404
          code    = 'STG/NAVIGATION_NOT_FOUND'
          message = |{ is_set-entity_type } has no navigation property { iv_name }|.
    ENDIF.
  ENDMETHOD.

  METHOD find_set.
    READ TABLE is_service-entity_sets INTO rs_set WITH KEY name = iv_entity_set.
    IF sy-subrc <> 0.
      RAISE EXCEPTION TYPE zcx_stg_error
        EXPORTING
          status  = 404
          code    = 'STG/ENTITY_SET_NOT_FOUND'
          message = |Entity set { iv_entity_set } does not exist in { is_service-name }|.
    ENDIF.
  ENDMETHOD.

  METHOD key_names.
    DATA ls_property LIKE LINE OF is_set-properties.

    LOOP AT is_set-properties INTO ls_property WHERE is_key = abap_true.
      APPEND ls_property-name TO rt_names.
    ENDLOOP.
  ENDMETHOD.

  METHOD find_property.
    DATA ls_property LIKE LINE OF is_set-properties.

    LOOP AT is_set-properties INTO ls_property.
      IF to_upper( ls_property-name ) = to_upper( iv_name ).
        rs_property = ls_property.
        RETURN.
      ENDIF.
    ENDLOOP.
    RAISE EXCEPTION TYPE zcx_stg_error
      EXPORTING
        status  = 400
        code    = 'STG/PROPERTY_NOT_FOUND'
        message = |Property { iv_name } does not exist in { is_set-entity_type }|.
  ENDMETHOD.

ENDCLASS.
